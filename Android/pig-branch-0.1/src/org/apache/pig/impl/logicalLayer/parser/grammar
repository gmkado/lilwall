/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

<query> := <expr> ";"

<expr> := [<alias> "="] <baseexpr> ["PARALLEL" <integer>]

<baseexpr> := <alias> | "LOAD" <loadclause> | "EVAL" <evalclause> | "FILTER" <filterclause> | ["CO"]"GROUP" <cogroupclause> | "CROSS" <nestedexprs> | "UNION" <nestedexprs> | "(" <baseexpr> ")"

<nestedexpr> := <alias> | "("<expr>")"

<nestedexprs> := <nestedexpr> ["," <nestedexprs>]

<loadclause> := <filename> ["USING" <loadfunc>"("[<loadfuncargs>]")"]

<loadfuncargs> := <constant> ["," <loadfuncargs>]

<cogroupclause> := <groupexpr> ["OUTER" | "INNER"]

<groupexpr> := "BY" <simpleprojitem> | "BY" <groupfunc>"("<simpleproj>")" | "ALL" | "ANY"

<simpleproj> := <simpleprojitem> ["," <simpleproj>]

<simpleprojitem>  := <field> | <constant> | "*"

<field> := "$"<integer>

<filename> := <quotedstring>

<constant> := <quotedstring>

<quotedstring> := "'"<string>"'"

<filterclause> := "FILTER" <filter> "OVER" <nestedexpr>

<filter> := <cond> "?" [<simpleproj> ":" <simpleproj>]

<cond> := <basecond> | <cond> "AND" <cond> | <cond> "OR" <cond> | "NOT" <cond> | "("<cond>")"

<basecond> := <comparable> <compareop> <comparable> | <comparable> "MATCHES" "'"<regex>"'" | <filterfunc>"("<simpleproj>")"

<comparable> := <field> | <constant>

<compareop> := "==" | "!=" | "<" | "<=" | ">" | ">="

<evalclause> := "EVAL" <evalitemlist> "OVER" <nestedexpr>

<evalitemlist> := (<evalitem> | (<nestableevalitem> ("["<innerevalspec>"]")* ["."<subproj>])) ["," <evalitemlist>]

<innerevalspec> := ["EVAL"] <simpleproj> | "FILTER" <filter> | <sad>

<subproj> := <subprojitem> | "(" <subprojitem> ("," <subprojitem>)* ")"

<subprojitem> := "*" | <field>

<nestableevalitem> := <field> | <alias> | <evalfunc>"("<argslist>")"

<argslist> := (<evalitem> | (<nestableevalitem> ("["<innerevalspec>"]")*) ["," <argslist>]

<evalitem> := <constant> | "*"

<sad> := ("SORT" | "ARRANGE") "BY" <simpleproj> | "DISTINCT" [<simpleproj>]
